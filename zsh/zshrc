bindkey -v

HISTSIZE=10000
SAVEHIST=10000

setopt extended_history \
    hist_ignore_space hist_ignore_dups hist_ignore_all_dups \
    hist_save_no_dups hist_expire_dups_first

zstyle ":completion:" rehash true
zstyle ":completion:*:default" menu select=1

fpath+=(/opt/homebrew/share/zsh/site-functions)
autoload -Uz compinit && compinit

maybe_source() { [[ -f "$1" ]] && source "$1"; }
if_installed() { command -v "$1" > /dev/null; }

alias ls="ls --color -F -A"
if_installed eza && alias ls="eza -F -A --icons"
if_installed nvim && alias vim="nvim"
if_installed kubectl && alias k="kubectl"
if_installed kubecolor && alias kubectl="kubecolor" && compdef kubecolor=kubectl

maybe_source /opt/homebrew/share/zsh-autopair/autopair.zsh
maybe_source /opt/homebrew/share/zsh-system-clipboard/zsh-system-clipboard.zsh
maybe_source /opt/homebrew/share/zsh-autosuggestions/zsh-autosuggestions.zsh
maybe_source /opt/homebrew/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

if_installed fzf && source <(fzf --zsh)
if_installed direnv && eval "$(direnv hook zsh)"

unset maybe_source if_installed

precmd() {
    PROMPT="%(?.%F{green}.%F{red})➜ "
    [[ -n "$SSH_CONNECTION" ]] && PROMPT+="%F{green}%m "
    PROMPT+="%F{cyan}"

    _repo_path="$(command -v git &> /dev/null \
        && git rev-parse --show-toplevel 2> /dev/null)"

    if [[ -n "$_repo_path" ]]; then
        PROMPT+="$(basename "$_repo_path")"
        [[ "$(pwd)" != "$_repo_path" ]] && PROMPT+="/${"$(pwd)"#$_repo_path/}"

        [[ -n "$(git status --porcelain)" ]] && _dirty="×"
        _branch="$(git branch --show-current)"
        [[ -z "$_branch" ]] && _branch="$(git rev-parse --short HEAD)"
        PROMPT+=" %F{magenta}git:(%F{red}$_branch%F{yellow}$_dirty%F{magenta}) "

        unset _repo_path _dirty _branch
    else
        PROMPT+="%~ "
    fi

    PROMPT+="%f"
}

precmd
